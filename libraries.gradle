subprojects {
  repositories {
    mavenCentral()
  }
  configurations.all {
    resolutionStrategy {
      // AGPL-compatible alternatives (mostly Apache v2)
      def replacements = [
        'org.jboss.spec.javax.annotation:jboss-annotations-api_1.1_spec': 'org.apache.geronimo.specs:geronimo-annotation_1.1_spec:1.0.1',
        'javax.activation:activation': 'org.apache.geronimo.specs:geronimo-activation_1.1_spec:1.1',
        // Use an Apache-licensed clean-room reimplementation of JCIP Annotations.
        // The original JCIP Annotations are under Creative Commons Attribution License
        // which is not recommended for software, and incompatible with AGPL.
        // https://wiki.creativecommons.org/Frequently_Asked_Questions#Can_I_apply_a_Creative_Commons_license_to_software.3F
        'net.jcip:jcip-annotations': 'com.github.stephenc.jcip:jcip-annotations:1.0-1',
      ]
      eachDependency { DependencyResolveDetails details ->
        def key = details.requested.group + ':' + details.requested.name
        def replacement = replacements[key]
        if (replacement) {
          details.useTarget(replacement)
        }
      }
    }
  }
}

def guiceVersion = '4.1.0'
def jacksonVersion = '2.8.1'
def log4jVersion = '2.6.2'
def resteasyVersion = '3.0.18.Final'

ext.libs = [
  guava:               dependencies.create('com.google.guava:guava:19.0') {
      force = true
    },
  jsr305:              'com.google.code.findbugs:jsr305:3.0.1',

  immutables:          'org.immutables:value:2.2.10',
  immutables_annotations: 'org.immutables:value:2.2.10:annotations',

  forbiddenapis:       'de.thetaphi:forbiddenapis:2.2',

  bouncycastle:        'org.bouncycastle:bcprov-jdk15on:1.54',

  jose4j:              'org.bitbucket.b_c:jose4j:0.5.1',

  guice:               [
      "com.google.inject:guice:$guiceVersion",
      "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    ],

  jongo:               [
      'org.jongo:jongo:1.3.0',
      'de.undercouch:bson4jackson:2.7.0',
      'org.mongodb:mongo-java-driver:3.2.2',
    ],

  elasticsearch:       [
      'org.elasticsearch:elasticsearch:1.4.4',
      'io.searchbox:jest:1.0.0'
    ],

  resteasy_netty:      [
      "org.jboss.resteasy:resteasy-netty:$resteasyVersion",
      'io.netty:netty:3.9.9.Final',
    ],

  resteasy_client:     [
      "org.jboss.resteasy:resteasy-client:$resteasyVersion",
      'net.ltgt.jaxrs:resteasy-client-okhttp3:1.0',
      "com.squareup.okhttp3:okhttp:3.4.1",
      "com.squareup.okhttp3:logging-interceptor:3.4.1"
    ],

  resteasy_testing:   'net.ltgt.jaxrs:resteasy-testing:0.2',

  jaxrs_webhook:      'net.ltgt.jaxrs:jaxrs-webhook:0.1',

  closure_templates_standalone: [
      'com.google.template:soy:2015-04-10',
      'args4j:args4j:2.33',
    ],
  closure_templates:   [
      'com.google.template:soy:2015-04-10',
      "com.google.inject.extensions:guice-assistedinject:$guiceVersion",
      "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    ],

  jackson_annotations: "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
  jackson_joda: "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion",
  jackson:             [
      "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
      "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
      "com.fasterxml.jackson.datatype:jackson-datatype-guava:$jacksonVersion",
      "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:$jacksonVersion",
      "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion",
      "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jacksonVersion",
    ],

  jdeb:                'org.vafer:jdeb:1.5',

  joda_time:           'joda-time:joda-time:2.9.4',

  jsSHA:               'org.webjars.bower:jsSHA:2.0.2',

  icu4j:               'com.ibm.icu:icu4j:57.1',

  galimatias:          'io.mola.galimatias:galimatias:0.2.1',

  mail:                'org.apache.geronimo.javamail:geronimo-javamail_1.4_mail:1.8.4',

  slf4j:               'org.slf4j:slf4j-api:1.7.21',

  log4j2:              "org.apache.logging.log4j:log4j-core:$log4jVersion",

  log4j_runtime:       [
      "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
      'com.lmax:disruptor:3.3.5',
    ],

  args4j:              'args4j:args4j:2.33',

  config:              'com.typesafe:config:1.2.1',

  junit:               'junit:junit:4.12',

  mockito:             'org.mockito:mockito-core:1.10.19',

  jukito:              [
      'org.jukito:jukito:1.4.1',
      "com.google.inject.extensions:guice-assistedinject:$guiceVersion",
    ],

  assertj:             [
      'org.assertj:assertj-core:2.5.0',
      'org.assertj:assertj-guava:2.0.0',
      'org.assertj:assertj-joda-time:1.1.0',
    ],
]

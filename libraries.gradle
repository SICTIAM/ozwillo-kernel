subprojects {
  repositories {
    mavenCentral()
  }
  dependencies {
    modules {
      module("javassist:javassist") {
        replacedBy("org.javassist:javassist")
      }
    }
  }
}

def guiceVersion = '4.0-beta5'
def immutablesVersion = '1.1.3'
def jacksonVersion = '2.5.1'
def log4jVersion = '2.2'
def resteasyVersion = '3.0.6.Final'
def swaggerVersion = '1.3.5'

ext.libs = [
  guava:               'com.google.guava:guava:18.0',
  jsr305:              'com.google.code.findbugs:jsr305:3.0.0',

  immutables:          "org.immutables:value:${immutablesVersion}",
  immutables_compiler: "org.immutables:value-standalone:${immutablesVersion}",

  bouncycastle:        'org.bouncycastle:bcprov-jdk15on:1.52',

  google_oauth_client: dependencies.create('com.google.oauth-client:google-oauth-client-java6:1.20.0') {
      exclude module: 'httpclient'
    },
  google_http_client:  dependencies.create('com.google.http-client:google-http-client-jackson2:1.20.0') {
      exclude module: 'httpclient'
    },

  guice:               [
      "com.google.inject:guice:$guiceVersion",
      "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    ],

  jongo:               [
      'org.jongo:jongo:1.1',
      'de.undercouch:bson4jackson:2.4.0',
      'org.mongodb:mongo-java-driver:2.13.0',
    ],

  elasticsearch:       [
      'org.elasticsearch:elasticsearch:1.4.4',
      'io.searchbox:jest:0.1.5'
    ],

  resteasy_netty:      [
      dependencies.create("org.jboss.resteasy:resteasy-netty:$resteasyVersion") {
        exclude module: 'slf4j-simple'
        exclude module: 'resteasy-jackson2-provider'
        exclude module: 'scannotation'
      },
      'io.netty:netty:3.9.6.Final',
    ],

  resteasy_client:     dependencies.create("org.jboss.resteasy:resteasy-client:$resteasyVersion") {
      exclude module: 'scannotation'
    },

  okhttp:             'com.squareup.okhttp:okhttp:2.3.0',

  closure_templates_standalone: [
      'com.google.template:soy:2015-03-06',
      'args4j:args4j:2.0.31',
    ],
  closure_templates:   [
      'com.google.template:soy:2015-03-06',
      "com.google.inject.extensions:guice-assistedinject:$guiceVersion",
      "com.google.inject.extensions:guice-multibindings:$guiceVersion",
    ],

  jackson_annotations: "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
  jackson_joda: "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion",
  jackson:             [
      "com.fasterxml.jackson.core:jackson-core:$jacksonVersion",
      "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
      "com.fasterxml.jackson.datatype:jackson-datatype-guava:$jacksonVersion",
      "com.fasterxml.jackson.jaxrs:jackson-jaxrs-base:$jacksonVersion",
      "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:$jacksonVersion",
      "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:$jacksonVersion",
    ],

  jdeb:                'org.vafer:jdeb:1.4',

  joda_time:           'joda-time:joda-time:2.7',

  icu4j:               'com.ibm.icu:icu4j:54.1.1',

  galimatias:          'io.mola.galimatias:galimatias:0.2.1',

  mail:                'com.sun.mail:javax.mail:1.5.2',

  // Keep version in sync with the one implemented by log4j-slf4j-impl, or it'll fail at runtime
  slf4j:               'org.slf4j:slf4j-api:1.7.7',

  log4j2:              "org.apache.logging.log4j:log4j-core:$log4jVersion",

  log4j_runtime:       [
      "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion",
      'com.lmax:disruptor:3.3.2',
    ],

  // Swagger
  swagger_annotations: "com.wordnik:swagger-annotations:$swaggerVersion",
  swagger_jaxrs:       [
      dependencies.create("com.wordnik:swagger-jaxrs_2.10:$swaggerVersion") {
        exclude module: 'jsr311-api'  // conflicts with org.jboss.resteasy:jaxrs-api
        exclude module: 'mockito-all' // conflicts with mockito-core
      },
      'javax.servlet:servlet-api:2.5'
    ],
  swagger_ui:          'org.webjars:swagger-ui:2.0.17',

  args4j:              'args4j:args4j:2.0.31',

  config:              'com.typesafe:config:1.2.1',

  junit:               'junit:junit:4.12',

  mockito:             'org.mockito:mockito-core:1.10.19',

  jukito:              [
      'org.jukito:jukito:1.4.1',
      "com.google.inject.extensions:guice-assistedinject:$guiceVersion",
    ],

  assertj:             [
      'org.assertj:assertj-core:2.0.0',
      'org.assertj:assertj-guava:1.3.1',
      'org.assertj:assertj-joda-time:1.1.0',
    ],
]


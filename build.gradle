apply from: './libraries.gradle'

group = 'fr.pole-numerique.oasis'
version = 'git describe --long'.execute(null, rootDir).text.trim() - ~'^v'

ext.versionlessOutputs = project.hasProperty("versionlessOutputs") ? Boolean.parseBoolean(project.property("versionlessOutputs")) : false;

subprojects {
  apply plugin: 'com.github.hierynomus.license'
  license {
    header = rootProject.file('LICENSE.header')
    skipExistingHeaders = true
    include '**/*.java'
    include '**/*.soy'
    mapping soy: 'JAVADOC_STYLE'

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'The Ozwillo Kernel Authors'
  }
  downloadLicenses {
    ext.apacheTwo = license('Apache License, Version 2.0', 'https://www.apache.org/licenses/LICENSE-2.0')
    ext.bsd = license('BSD License', 'http://www.opensource.org/licenses/bsd-license.php')
    ext.mit = license('MIT License', 'http://www.opensource.org/licenses/mit-license.php')

    licenses = [
      // libraries under several licenses: pick one
      (group('org.javassist')):   apacheTwo,
      (group('org.reflections')): bsd,
    ]
    aliases = [
      (apacheTwo): [ 'Apache License, Version 2.0', 'The Apache Software License, Version 2.0', 'ASL', 'Apache 2', 'Apache 2.0', 'Apache License 2.0' ],
      (bsd):       [ 'The New BSD License', 'BSD-like', 'BSD', 'BSD licence'/*sic!*/ ],
      (mit):       [ 'Bouncy Castle Licence', 'ICU License', 'MIT License' ],
    ]
  }

  apply plugin: 'net.ltgt.apt'

  apply plugin: 'net.ltgt.errorprone'
  configurations.errorprone {
    resolutionStrategy.force "com.google.errorprone:error_prone_core:${JavaVersion.current().isJava8Compatible() ? '2.0.15' : '2.0.5'}"
  }

  apply plugin: 'idea' // IDEA needs extra configuration for generated sources *sigh*

  group = rootProject.group
  version = rootProject.version

  plugins.withType(JavaPlugin).whenPluginAdded {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    project.apply plugin: 'de.thetaphi.forbiddenapis'
    forbiddenApis {
      bundledSignatures = [ 'jdk-unsafe', 'jdk-deprecated', 'jdk-system-out' ]
    }

    tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
      options.compilerArgs += [ '-Werror', '-Xlint:all', '-Xlint:-processing', '-Xlint:-serial', '-Xlint:-path' ]
    }

    if (versionlessOutputs) {
      tasks.withType(AbstractArchiveTask) {
        archiveName = "${baseName}.${extension}"
      }
    }
  }
}

buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.9'
    classpath('gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1') {
      exclude group: 'com.android.tools.build', module: 'gradle'
    }
    classpath 'de.thetaphi:forbiddenapis:2.2'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.2.1'
}

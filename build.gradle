apply from: './libraries.gradle'

group = 'fr.pole-numerique.oasis'
version = 'git describe --long'.execute(null, rootDir).text.trim() - ~'^v'

ext.versionlessOutputs = project.hasProperty("versionlessOutputs") ? Boolean.parseBoolean(project.property("versionlessOutputs")) : false;

subprojects {
  apply plugin: 'net.ltgt.apt'

  apply plugin: 'net.ltgt.errorprone'
  configurations.errorprone {
    resolutionStrategy.force 'com.google.errorprone:error_prone_core:2.0.1'
  }

  apply plugin: 'idea' // IDEA needs extra configuration for generated sources *sigh*

  group = rootProject.group
  version = rootProject.version

  plugins.withType(JavaPlugin).whenPluginAdded {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    tasks.withType(JavaCompile) {
      options.encoding = 'UTF-8'
    }

    if (versionlessOutputs) {
      tasks.withType(AbstractArchiveTask) {
        archiveName = "${baseName}.${extension}"
      }
    }
  }
}

buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2'
    }
  }
  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.7'
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.3'
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.4'
}
